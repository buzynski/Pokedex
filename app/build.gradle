plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {

    signingConfigs {
        def keystorePropertiesFile = rootProject.file("keystore.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        key {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    defaultConfig {
        applicationId "com.buzynski.pokedex"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.key
    }

    // ---

    variantFilter {
        def names = it.flavors*.name
        ignore = buildType.name == "qa" && names.contains("apiMocked")
    }

    // ---

    flavorDimensions "pokedex"

    // ---

    productFlavors {
        apiProduction {
            dimension "pokedex"
            applicationIdSuffix ".a"

            resValue "string", "app_name", "Pokedex A"

            buildConfigField('String', 'API_URL', '"https://pokeapi.co/api/v2/"')
            buildConfigField('String', 'API_FRONT_IMG_URL', '"https://raw.githubusercontent.com/PokeAPI/sprites/master/sprites/pokemon/versions/generation-v/black-white/animated/"')
            buildConfigField('String', 'API_BACK_IMG_URL', '"https://raw.githubusercontent.com/PokeAPI/sprites/master/sprites/pokemon/versions/generation-v/black-white/animated/back/"')
        }

        apiMocked {
            dimension "pokedex"
            applicationIdSuffix ".b"

            resValue "string", "app_name", "Pokedex B"

            buildConfigField('String', 'API_URL', '"https://pokeapi.co/api/v1/"')
            buildConfigField('String', 'API_FRONT_IMG_URL', '"https://raw.githubusercontent.com/PokeAPI/sprites/master/sprites/pokemon/versions/generation-v/black-white/animated/"')
            buildConfigField('String', 'API_BACK_IMG_URL', '"https://raw.githubusercontent.com/PokeAPI/sprites/master/sprites/pokemon/versions/generation-v/black-white/animated/back/"')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        qa {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // ---

    // KOIN
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    // COROUTINES
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    // LIVE DATA
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"

    // RECYCLERVIEW
    implementation "androidx.recyclerview:recyclerview:1.2.0-beta02"

    // NAVIGATION
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.4"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.4"

    // GLIDE
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    // LEAKCANARY
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // ---
}